(ns status-im.test.protocol.core
  (:require [cljs.test :refer-macros [deftest is testing run-tests
                                      async use-fixtures]]
            [cljs.nodejs :as nodejs]
            [cljs.core.async :as async]
            [status-im.protocol.web3.utils :as web3.utils]
            [status-im.test.protocol.node :as node]
            [status-im.test.protocol.utils :as utils]
            [status-im.protocol.core :as protocol]))

;; NOTE(oskarth): All these tests are evaluated in NodeJS

(nodejs/enable-util-print!)

(def rpc-url (aget nodejs/process.env "WNODE_ADDRESS"))

(def Web3 (js/require "web3"))
(defn make-web3 []
  (Web3. (Web3.providers.HttpProvider. rpc-url)))

(defn make-callback [identity done]
  (fn [& args]
    (is (contains? #{:sent :pending} (first args)))
    (when (= (first args) :sent)
      (protocol/reset-all-pending-messages!)
      (protocol/stop-watching-all!)
      (done)
      (utils/exit!))))

(defn post-error-callback [identity]
  (fn [& args]
    (.log js/console (str :post-error " " identity "\n" args))))

(defn id-specific-config
  [id {:keys [private public]} contacts done]
  {:identity            id
   :callback            (make-callback id done)
   :profile-keypair     {:public  public
                         :private private}
   :contacts            contacts
   :post-error-callback (post-error-callback id)})

(defn ensure-test-terminates! [timeout done]
  (async/go (async/<! (async/timeout (* timeout 1000)))
            (println "ERROR: Timeout of" timeout  "seconds reached - failing test.")
            (is (= :terminate :indeterminate))
            (done)))

(defn create-keys [shh]
  (let [keypair-promise (.newKeyPair shh)]
    (.getPublicKey shh keypair-promise)))

(deftest test-send-message!
  (testing "sending messages"
    (async done
           (let [timeout       30
                 web3          (make-web3)
                 shh  (web3.utils/shh web3)
                 id1-keypair   (protocol/new-keypair!)
                 from          (create-keys shh)
                 common-config {:web3                        web3
                                :groups                      []
                                :ack-not-received-s-interval 125
                                :pow-target                  0.001
                                :pow-time                    1
                                :default-ttl                 120
                                :send-online-s-interval      180
                                :ttl-config                  {:public-group-message 2400}
                                :max-attempts-number         3
                                :delivery-loop-ms-interval   500
                                :hashtags                    []
                                :pending-messages            []}
                 id1-config    (id-specific-config from id1-keypair [] done)]
             (ensure-test-terminates! timeout done)
             (protocol/init-whisper! (merge common-config id1-config))
             (protocol/send-message!
               {:web3    web3
                :message {:message-id "123"
                          :from       from
                          :to         node/identity-2
                          :payload    {:type         :message
                                       :content-type "text/plain"
                                       :content      "123"
                                       :timestamp    1498723691404}}})))))

(deftest test-whisper-version!
  (testing "Whisper version supported"
    (async done
           (let [web3 (make-web3)
                 shh  (web3.utils/shh web3)]
             (.version shh
                       (fn [& args]
                         (is (= "6.0" (second args)))
                         (done)))))))
